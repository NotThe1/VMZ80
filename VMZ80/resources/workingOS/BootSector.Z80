;     File created by MakeZ80Source on Tue Sep 18 10:01:22 EDT 2018 from:
;     C:\Users\admin\Dropbox\Resources\CPM\CurrentOS\8080\BootSector.asm
 ; BootSector.Z80 
 ; Build 2.01

 ; 2018-02-19 BDOS 1.0 designation 
 ; 2018-11-22 begin  Z80 idiom update
 ; 2017-04-07 Added equates for message IO : BOOT_CON_ADDRESS
 ;				 , and default IOBYTE : IOBYTE_DEFAULT
 ; 2017-03-02 Refactored the CP/M Suite
 
 	DollarInclude ./stdHeader.Z80
 	$Include ./osHeader.Z80
 	$Include ./diskHeader.Z80
CR				EQU		0DH			; Carriage Return
EndOfMessage	EQU		00H
LF				EQU		0AH			; Line Feed

 	
BOOT_CON_ADDRESS	EQU	0ECH			; tty data address
IOBYTE_DEFAULT		EQU	080H			; LST:=LPT, rest all = TTY:


WarmBootEntry		EQU		BIOSStart + 3

					ORG    TPA
Start:
 	LD		SP,Start-1					; Stack goes down from here
 	CALL	SendBootMessage				; Display boot message
 	LD		BC,Page0ImageEnd-Page0Image	; Size of code to move
 	LD		HL,Page0Image				; Source of page 0 code
 	LD		DE,0000						; Location 0, the target
 
; Set up page zero,Move (B) bytes from (HL) to (DE).
	LDIR
 
; Now  start to move data to Disk Control Block
 
 	LD		HL,BootControl
 	LD		(DiskCommandBlock),HL		; Set up Command block for drive A:
 
 
 	LD		HL,DiskControlByte
 	LD		(HL),080H					; Activate the controller
	
WaitForBootComplete:
 	LD		A,(HL)						; Get the control byte
 	OR		A							; 0 = Completed operation
 	JR		NZ,WaitForBootComplete		;  if not try again
 
 	LD		A,(DiskStatusLocation)		; What's the status after operation
 	CP		080H						; Any errors ?
	
	
 	JP		NC,0000						; No, do a Warm Boot
										;  else we have a problem	
	LD		HL,FailedBootMessage
 	CALL	SendMessage								
 	HALT

	
;---------------------------- ROM Disk Control Block ------------------------; 
BootControl:                                                                 ;
 	DB		DiskReadCode				; Read function                   	 ;
 	DB		00H							; unit number                     	 ;
 	DB		00H							; head number                     	 ;
 	DB		00H							; track number                    	 ;
 	DB		0DH							; Starting sector number (13)     	 ;
 	DW		5 * 512						; Number of bytes to read         	 ;
										;	( 0A00 All of BIOS)           	 ;
 	DW		BIOSStart					; read into this address          	 ;
 	DW		DiskStatusLocation			; next block - no linking         	 ;
 	DW		DiskControlTable			; next table- no linking          	 ;
;---------------------------- ROM Disk Control Block ------------------------;

;------------------------------------ Page 0 --------------------------------; 
Page0Image:                                                                  ;
 	JP		WarmBootEntry				; warm start                         ;
;IOBYTE:                                                                     ;
 	DB		IOBYTE_DEFAULT				; IOBYTE-                            ;
DefaultDisk:                                                                 ;
 	DB		00H							; Current default drive (A)          ;
 	JP		BDOSEntry					; jump to BDOS entry                 ;
 	DS		028H						; interrupt locations 1-5 not used   ;
 	DS		008H						; interrupt location 6 is reserved   ;
 	JP		0000H						; RST 7 used only by DDT & SID       ;
Page0ImageEnd:                                                               ;
;------------------------------------ Page 0 --------------------------------; 
 
;---------------------------------- Messages --------------------------------;  
BootMessage:                                                                 ;
 	DB		CR,LF                                                            ;
 	DB		'BootStrap loader '                                              ;
	DB		'CP/M 2.2 (Z80) '                                                ;
	DB		CR,LF                                                            ;
	DB		'Build 0.D '                                                     ;
	DB		CR,LF                                                            ;
	DB		'CCP 0.A |  BDOS 1.0 | BIOS 1.0'                                 ;
	DB		CR,LF,CR,LF,EndOfMessage                                         ;
	                                                                         ;
FailedBootMessage:                                                           ;
	DB		CR,LF                                                            ;
	DB		'Boot Unsuccessful - Check for Disk in A:'                       ;
 	DB		CR,LF,EndOfMessage                                               ;
                                                                             ;
SendBootMessage:                                                             ;
 	LD		HL,BootMessage                                                   ;
	                                                                         ;
SendMessage:                                                                 ;
 	LD		A,(HL)                                                           ;
 	OR		A                                                                ;
 	RET		Z                                                                ;
 	OUT		(BOOT_CON_ADDRESS),A		; Console address		             ;
 	INC		HL                                                               ;
 	JP		SendMessage                                                      ;
;---------------------------------- Messages --------------------------------;
Z_HighestLocation:
Z_MemoryLeft			EQU    0512H - Z_HighestLocation
